// Overriding pest configuration
WHITESPACE = _{ WHITE_SPACE }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
eoi = _{ !ANY }

// Numbers used in type level
number = @{ ASCII_DIGIT+ }
// File path used in import
filename = @{ ( ASCII_ALPHANUMERIC | PUNCTUATION )+ }
// String used in variable an module names
string = @{ !keywords ~ ASCII_ALPHA ~ ( "_" | ASCII_ALPHANUMERIC )* }
// Keywords that should be used by variable and module names
keywords = @{ ( "super" | "use" | "end" | "mod" | "fun" | "def" | "check" | "Prop" | "Type" ) ~ !ASCII_ALPHANUMERIC }
// Special keywords
public = @{ "pub" }
superkw = @{ "super" }

// Lambda terms
Term = _{ Abs | dProd | Prod | App | Var | Prop | Type | "(" ~ Term ~ ")" }
term_prod = _{ App | Abs | dProd | Var | Prop | Type | "(" ~ Prod ~ ")" | "(" ~ term_prod ~ ")" }
term_app = _{ Abs | Var | Prop | Type | "(" ~ App ~ ")" | "(" ~ Prod ~ ")" | "(" ~ dProd ~ ")" | "(" ~ term_app ~ ")" } 
term_dprod = _{ App | Abs | Prod | dProd | Var | Prop | Type | "(" ~ term_dprod ~ ")" }
term_abs = _{ App | Abs | Prod | dProd | Var | Prop | Type | "(" ~ term_abs ~ ")" }

// Abstractions
Abs = { ( "fun" ~ ( arg_abs_par ~ ( "," ~ arg_abs_par )* ) ~ "=>" ~ Term ) }
arg_abs_par = _{ arg_abs | "(" ~ arg_abs_par ~ ")" }
arg_abs = { string+ ~ ":" ~ term_abs }

// Dependant product
dProd = { "(" ~ ( arg_dprod_par ~ ( "," ~ arg_dprod_par )* ) ~ ")" ~ "->" ~ Term }
arg_dprod_par = _{ arg_dprod | "(" ~ arg_dprod_par ~ ")" }
arg_dprod = { string+ ~ ":" ~ term_dprod }

// Application
App = { term_app ~ term_app+ }
Prod = { term_prod ~ ( "->" ~ term_prod )+ }

// Proposition
Prop = { "Prop" }
// Type
Type = { "Type(" ~ number ~ ")" | "Type" ~ number? }
// Variable
Var = ${ (superkw ~ "::")* ~ (string ~ "::")* ~ string }

// Commands
Command = _{ Define | CheckType | GetType | DefineCheckType | Eval | ImportFile | Search | BeginModule | EndModule | UseModule }
Define = { ( public? ) ~ "def" ~ string ~ ":=" ~ Term }
DefineCheckType = { ( public? ) ~ "def" ~ string ~ ":" ~ Term ~ ":=" ~ Term }
CheckType = { "check" ~ Term ~ ":" ~ Term }
GetType = { "check" ~ Term }
Eval = { "eval" ~ Term }
ImportFile = { "import" ~ filename* }
Search = { "search" ~ string }
BeginModule = { "mod" ~ string }
EndModule = { "end" }
UseModule = { "use" ~ Var }

// Definitions used to generate parsers for lines and files
command = _{SOI ~ Command ~ eoi }
file = _{ SOI ~ Command* ~ eoi }
