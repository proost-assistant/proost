WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
number = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
string = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

Term = _{ Prop | Type | Var | App | Abs | Prod | ParTerm }
Prop = { "P" }
Type = { "T(" ~ number ~ ")" }
Var = { string }
App = { "(" ~ Term ~ ")" ~ Term }
Abs = { "/\\" ~ string ~ ":" ~ Term ~ "->" ~ Term }
Prod = { "\\/" ~ string ~ ":" ~ Term ~ "->" ~ Term }
ParTerm = _{ "(" ~ Term ~ ")" }

Command = _{ ( Define | CheckType | GetType ) ~ "." }
Define = { "Define" ~ string ~ ":=" ~ Term }
CheckType = { ( "CheckType" | "Check" ) ~  Term ~ ":" ~ Term }
GetType = { ( "Type" | "GetType" ) ~ Term }

File = { SOI ~ (NEWLINE* ~ Command)* ~ NEWLINE* ~ EOI }
